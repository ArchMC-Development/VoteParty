import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'org.jetbrains.kotlin.jvm' version '1.8.10'
	id 'com.github.johnrengelman.shadow' version '7.1.2' apply false
	id "com.github.ben-manes.versions" version "0.46.0"
}

allprojects {
	apply plugin: 'org.jetbrains.kotlin.jvm'

	archivesBaseName = 'VoteParty'
	group 'me.clip'
	version '2.35-DEV'

	repositories {
		mavenCentral()

		maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
		maven { url = "https://repo.aikar.co/content/groups/aikar/" }
		maven { url = "https://papermc.io/repo/repository/maven-public/" }
		maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
		maven { url = "https://repo.glaremasters.me/repository/public/" }
	}

	dependencies {
		// kotlin
		implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

		// spigot
		compileOnly "org.spigotmc:spigot-api:1.19.3-R0.1-SNAPSHOT"
	}

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	compileJava {
		options.compilerArgs += ["-parameters"]
		options.fork = true
		options.forkOptions.executable = 'javac'
	}

	compileKotlin {
		kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
		kotlinOptions.javaParameters = true
	}
}

apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
	minimize()
	relocate("co.aikar.commands", "me.clip.voteparty.libs.acf")
	relocate("co.aikar.locales", "me.clip.voteparty.libs.locales")
	relocate("ch.jalu.configme", "me.clip.voteparty.libs.configme")
	relocate("org.inventivetalent", "me.clip.voteparty.libs.inventivetalent")
	relocate("net.kyori", "me.clip.voteparty.libs.kyori")
	relocate("com.cryptomorin.xseries", "me.clip.voteparty.libs.xseries")
	relocate("kotlin", "me.clip.voteparty.libs.kotlin")
	relocate("co.aikar.idb", "me.clip.voteparty.libs.idb")
	relocate("com.zaxxer", "me.clip.voteparty.libs.hikari")
	archiveFileName = "VoteParty-${project.version}.jar"
}


dependencies {

	// config
	implementation "ch.jalu:configme:1.3.0"

	// placeholderapi
	compileOnly "me.clip:placeholderapi:2.11.2"

	// NuVotifier hook
	compileOnly files("libs/nuvotifier-2.7.3.jar")

	// command handler
	implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"

	// Support for materials across all versions
	implementation 'com.github.cryptomorin:XSeries:9.3.0'

	// json stuff
	implementation 'net.kyori:adventure-platform-bukkit:4.2.0'
	implementation 'net.kyori:adventure-api:4.12.0'
	implementation 'net.kyori:adventure-text-minimessage:4.12.0'

	// MySQL?
	implementation "co.aikar:idb-core:1.0.0-SNAPSHOT"
	implementation "com.zaxxer:HikariCP:4.0.3"

	implementation project(":version")
	implementation project(":version_old")
	implementation project(":version_new")
}

processResources {
	filter ReplaceTokens, tokens: [version: version]
}
